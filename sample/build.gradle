apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 19
    buildToolsVersion '21.1.2'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"

        //check for properties in the gradle file, and warn the user
        for (String property : ["TEST_USER_EMAIL","TEST_USER_PASSWORD","TEST_OAUTH_CONSUMER_KEY","TEST_OAUTH_CONSUMER_SECRET"]) {
            if (!project.properties.containsKey(property)) {
                throw new IllegalArgumentException("${property} property must be added to the projects gradle.properties file in order to" +
                        " run the sample. Refer to the readme for more information.")
            }
        }

        buildConfigField "String", "TEST_USER_EMAIL", "$TEST_USER_EMAIL"
        buildConfigField "String", "TEST_USER_PASSWORD", "$TEST_USER_PASSWORD"
        buildConfigField "String", "TEST_OAUTH_CONSUMER_KEY", "$TEST_OAUTH_CONSUMER_KEY"
        buildConfigField "String", "TEST_OAUTH_CONSUMER_SECRET", "$TEST_OAUTH_CONSUMER_SECRET"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        textReport true
    }

    //avoid conflicts with jackson mappers license files
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    compile project(':sdk')
    compile 'com.jakewharton:butterknife:6.+'
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.mcxiaoke.volley:library:1.0.10'
}
