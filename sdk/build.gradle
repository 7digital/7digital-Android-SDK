apply plugin: 'android-library'

android {
    compileSdkVersion 19
    buildToolsVersion "20.0.0"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
        versionCode 3
        versionName "0.92"

        buildConfigField "String", "TEST_USER_EMAIL", "\"TEST_USER_EMAIL\""
        buildConfigField "String", "TEST_USER_PASSWORD", "\"TEST_USER_PASSWORD\""
        buildConfigField "String", "TEST_OAUTH_CONSUMER_KEY", "\"TEST_OAUTH_CONSUMER_KEY\""
        buildConfigField "String", "TEST_OAUTH_CONSUMER_SECRET", "\"TEST_OAUTH_CONSUMER_SECRET\""

    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        textReport true
        disable 'InvalidPackage'

    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}
    android.libraryVariants.all { variant ->
        def name = variant.buildType.name
        if (name.equals(com.android.builder.core.BuilderConstants.DEBUG)) {
            return; // Skip debug builds.
        }

        /**
         * This task creates a (slim) jar that just includes the 7DI SDK
         */
        def slimJar = project.tasks.create "jar${name.capitalize()}", Jar
        slimJar.dependsOn variant.javaCompile
        slimJar.archiveName = "7digital-android-sdk-vX.XX.jar"
        slimJar.from variant.javaCompile.destinationDir

        /**
         * This task creates a fat jar that includes all compiled dependencies as well
         */
        def fatJar = project.tasks.create "jarWithDependencies${name.capitalize()}", Jar
        fatJar.dependsOn slimJar
        fatJar.from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
        fatJar.with slimJar

        //These files need to be excluded from the jar because they throw errors when an android project
        //references this jar
        fatJar.exclude 'classes.jar'
        fatJar.exclude 'AndroidManifest.xml'
        //set the output name
        fatJar.archiveName = "7digital-android-sdk-vX.XX-withDependencies.jar"


        artifacts.add('archives', slimJar);
    }

dependencies {

    compile('org.simpleframework:simple-xml:2.7.1') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    compile 'com.squareup.okhttp:okhttp:1.5.2'
    compile 'com.android.support:appcompat-v7:20.+'
    compile 'com.fasterxml.jackson.core:jackson-core:2.3.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.3.1'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.3.1'

    compile 'com.android.volley:volley:+@aar'

    compile 'joda-time:joda-time:2.3'
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
